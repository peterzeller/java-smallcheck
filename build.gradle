group 'com.github.peterzeller'
version '0.0.4'

// Apply the java-library plugin to add support for Java Library
apply plugin: 'java-library'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.bmuschko.nexus'

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'eu.appsatori.fatjar'

fatJar {
    exclude 'META-INF/*.DSA'
}

// In this section you declare where to find the dependencies of your project
repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    // advanced reflection utilities
    implementation 'io.leangen.geantyref:geantyref:1.3.4'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:21.0'

    // Use JUnit test framework
    api 'junit:junit:4.12'
}

compileJava.options.compilerArgs.add '-parameters'
compileTestJava.options.compilerArgs.add '-parameters'


/**
 * Publishing to maven central:
 *
 * 0. Configure once:
 *
 *    Edit ~/.gradle/gradle.properties and add secret information:
 *
 *          signing.keyId=0E1885EA
 *          signing.password=********
 *          signing.secretKeyRingFile=/path/to/gnupg/secring.gpg
 *
 *          ossrhUsername=pzeller
 *          ossrhPassword=***********
 *
 *
 * 1. Run ./gradlew uploadArchives
 * 2. Go to https://oss.sonatype.org/ and sign in
 * 3. Go to "Staging Repositories" and find project
 * 4. Close repository
 * 5. Release repository
 *
 */

modifyPom {
    project {
        name 'Java SmallCheck'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'An implementation of SmallCheck for Java as a JUnit extension.'
        url 'http://www.example.com/example-application'

        scm {
            connection 'scm:https://github.com/peterzeller/java-smallcheck'
            developerConnection 'scm:https://github.com/peterzeller/java-smallcheck'
            url 'https://github.com/peterzeller/java-smallcheck'
        }

        licenses {
            license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            }
        }

        developers {
            developer {
                id 'pzeller'
                name 'Peter Zeller'
                email 'p_zeller@cs.uni-kl.de'
            }
        }
    }
}
